&ACCESS RV1
DEFFCT INT pvp_question ( )
   DECL KrlMsg_T askForMode
   DECL KrlMsgDlgSK_T modeSoftkey[2]
   DECL KrlMsgPar_T somethingParameter[3]
   DECL KrlMsgOpt_T modeOption
   DECL INT dialogHandle
   DECL INT dialogValue
   DECL BOOL dialogPresent
   
   ;FOLD SoftKeys
   modeSoftkey[1] = {sk_type #value, sk_txt[] "Robot."}
   modeSoftkey[2] = {sk_type #value, sk_txt[] "Player."}
   ;ENDFOLD SoftKeys
   
   ;FOLD Parameter
   somethingParameter[1] = {Par_Type #EMPTY}
   somethingParameter[2] = {Par_Type #EMPTY}
   somethingParameter[3] = {Par_Type #EMPTY}
   ;ENDFOLD
   
   modeOption = {VL_STOP FALSE, Clear_P_Reset TRUE, Clear_P_SAW FALSE, Log_to_DB TRUE}
   askForMode = {modul[] "Game mode" , Nr 2022, msg_txt[] "Want to play against a robot or player?"}
   
   dialogHandle = SET_KRLDLG(askForMode, somethingParameter[], modeSoftkey[], modeOption)
   dialogValue = 0
   dialogPresent = TRUE
   
   WHILE dialogPresent == TRUE
       dialogPresent = EXISTS_KRLDLG(dialogHandle, dialogValue)
   ENDWHILE
   
   RETURN (dialogValue)
ENDFCT